pluginManagement {
    repositories {
        gradlePluginPortal()
        mavenCentral()
        exclusiveContent {
            forRepository {
                maven {
                    name = "Fabric"
                    url = uri("https://maven.fabricmc.net")
                }
            }
            filter {
                includeGroup("net.fabricmc")
                includeGroup("fabric-loom")
            }
        }
        exclusiveContent {
            forRepository {
                maven {
                    name = "Sponge"
                    url = uri("https://repo.spongepowered.org/repository/maven-public")
                }
            }
            filter { includeGroupAndSubgroups("org.spongepowered") }
        }
        exclusiveContent {
            forRepositories(
                maven {
                    name = "NeoForge"
                    url = "https://maven.neoforged.net/releases"
                }
            )
            filter { includeGroupAndSubgroups("net.neoforged.licenser") }
        }
    }
}

plugins {
    // https://plugins.gradle.org/plugin/org.gradle.toolchains.foojay-resolver-convention
    id "org.gradle.toolchains.foojay-resolver-convention" version "0.8.0"
}

rootProject.name = "ClientSort"
include("common")
include("fabric")
include("neoforge")
