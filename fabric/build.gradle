plugins {
    id("multiloader-loader")
    id("fabric-loom")
}

dependencies {
    minecraft("com.mojang:minecraft:${minecraft_version}")
    mappings loom.layered {
        officialMojangMappings()
        parchment("org.parchmentmc.data:parchment-${parchment_minecraft_version}:${parchment_version}@zip")
    }

    // Fabric Loader and API
    modImplementation("net.fabricmc:fabric-loader:${fabric_loader_version}")
    modImplementation("net.fabricmc.fabric-api:fabric-api:${fabric_api_version}")

    // JiJ MixinExtras if required to be newer than minimum loader-provided version
//    include(implementation(annotationProcessor("io.github.llamalad7:mixinextras-fabric:${mixinextras_version}")))

    // ModMenu
    modImplementation("maven.modrinth:mOgUt4GM:${modmenu_version}")
    
    // Cloth Config API
    modApi("me.shedaniel.cloth:cloth-config-fabric:${clothconfig_version}") {
        exclude(group: "net.fabricmc.fabric-api")
    }

    // ItemLocks
    modImplementation("maven.modrinth:tJzrFuyy:${itemlocks_version}")
}

loom {
    def aw = project(":common").file("src/main/resources/${mod_id}.accesswidener")
    if (aw.exists()) accessWidenerPath.set(aw)
    mixin {
        defaultRefmapName.set("${mod_id}.refmap.json")
    }
    runs {
        client {
            client()
            setConfigName("Fabric Client")
            ideConfigGenerated(true)
            runDir("runs/client")
        }
        server {
            server()
            setConfigName("Fabric Server")
            ideConfigGenerated(true)
            runDir("runs/server")
        }
    }
}
